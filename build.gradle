plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.13.3'
    id "io.freefair.lombok" version "6.2.0"
    id "org.sonarqube" version "3.3"
}
apply plugin: 'java-library'
apply plugin: 'signing'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'



group = "com.stepstone.inbucket"
project.version = scmVersion.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

test.useTestNG()

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'

    testImplementation 'org.testng:testng:7.4.0'
    testImplementation 'com.squareup.retrofit2:retrofit-mock:2.9.0'

}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
    }
}

check.dependsOn jacocoTestReport

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'inbucket-client'
            from components.java
            pom {
                name = 'Inbucket Client'
                packaging = 'jar'
                description = 'Java client for inbucket'
                url = 'https://github.com/stepstone-tech/inbucket-java-client'

                scm {
                    connection = 'scm:git:git@github.com:stepstone-tech/inbucket-java-client.git'
                    developerConnection = 'scm:git:git@github.com:stepstone-tech/inbucket-java-client.git'
                    url = 'https://github.com/stepstone-tech/inbucket-java-client'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id = 'tommywo'
                        name = 'Tomasz Wojtu≈Ñ'
                        email = 'tomasz.wojtun@stepstone.com'
                    }
                }
            }

        }
    }
}


signing {
    sign publishing.publications.mavenJava
}

javadoc {
    failOnError = false
}

sonarqube {
    properties {
        property "sonar.projectKey", "stepstone-tech_inbucket-java-client"
        property "sonar.organization", "stepstone-tech"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}