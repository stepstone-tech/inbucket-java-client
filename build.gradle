plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.5.0'
}
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'


group = "com.stepstone.inbucket"
project.version = scmVersion.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

test.useTestNG()

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.18'
    compile 'com.squareup.retrofit2:retrofit:2.0.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.0.0'

    testCompile 'org.testng:testng:6.8.1'
    testCompile 'com.squareup.retrofit2:retrofit-mock:2.0.0'

}

task wrapper(type: Wrapper) {
 gradleVersion = '2.12'
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    reports {
        xml.enabled true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

signing {
    required project.hasProperty("signingEnabled")
    sign configurations.archives
}

artifacts {
    archives javadocJar, sourcesJar

}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: project.hasProperty('ossrhUsername')?ossrhUsername:'', password: project.hasProperty('ossrhPassword')?ossrhPassword:'')
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: project.hasProperty('ossrhUsername')?ossrhUsername:'', password: project.hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            pom.project {
                name 'Inbucket Client'
                packaging 'jar'
                description 'Java client for inbucket'
                url 'https://github.com/stepstone-tech/inbucket-java-client'

                scm {
                    connection 'scm:git:git@github.com:stepstone-tech/inbucket-java-client.git'
                    developerConnection 'scm:git:git@github.com:stepstone-tech/inbucket-java-client.git'
                    url 'https://github.com/stepstone-tech/inbucket-java-client'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'tommywo'
                        name 'Tomasz Wojtu≈Ñ'
                        email 'tomasz.wojtun@stepstone.com'
                    }
                }
            }
        }
    }
}
